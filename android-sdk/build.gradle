apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 27



    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    implementation 'com.birbit:android-priority-jobqueue:2.0.1'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.google.code.gson:gson:2.8.2'
}

group = 'podcast.statistics.collector'
version = '1.0.4'

install {
    repositories.mavenInstaller {
        pom.project {
            group "podcast.statistics.collector"
            artifactId "android-sdk"
            version project.version
        }
    }
}

ext {
    bintrayPassphrase = project.hasProperty('bintrayPassphrase') ? project.property('bintrayPassphrase') : System.getenv('BINTRAY_PASSPHRASE')
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    // jFrog plugin must be declared for this line to work
    configurations = ['archives']

    publish = true

    // Package info for BinTray
    pkg {
        repo = 'Android-SDK'
        name = 'podcast-statistics-collector'
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/PodcastStatisticsCollecting/Android-SDK.git'
        version {
            name = project.version
            desc = 'Android SDK'
            vcsTag = project.version

            gpg {
                sign = true
                passphrase = "${bintrayPassphrase}"
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}